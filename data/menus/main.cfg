menustatevar __main_setup_notice ""
menustatevar __ignore_version_warning 0
menusetting languageOffset 0

menusetting flagsPerPage 3

menusetting needSetup 0

newgui main [
    if (|| islastversion $__ignore_version_warning) [
        if (|| (!= -1 (indexof $unnamedList (getname))) (=s (getname) "")) [
            name (gettrans "name.none") //TODO: less hacky way
            needSetup = 1
        ]
        

        if $needSetup [
            guicenter [ guistayopen [
                guititle "t:gui.main.setup.title"
                guibar
                
                guibutton "" [
                    languageOffset = (- $languageOffset 1)
                    if (< $languageOffset 0) [
                        languageOffset = (- (listlen $supportedLanguages) $flagsPerPage)
                    ]
                ] "arrow_bw_minor"
                
                __flagsleft = $flagsPerPage
                __current_offset = 0
                
                loop i 2 [
                    looplist x $supportedLanguages [
                        if (< $__current_offset $languageOffset) [
                            __current_offset = (+ $__current_offset 1)
                            //skip
                        ] [
                            if (> $__flagsleft 0) [
                                __flagsleft = (- $__flagsleft 1)
                                guibutton "" [ changelanguage @x; guiheader (gettrans "gui.main.title") ] (getalias (concatword "LANGUAGE_" $x "_ICON"))
                            ]
                        ]
                    ]
                ]
                guibutton "" [
                    languageOffset = (+ $languageOffset 1)
                    if (= $languageOffset (listlen $supportedLanguages)) [
                        languageOffset = 0
                    ]
                ] "arrow_fw_minor"
            ] ]
            guicenter [
                guitext "t:gui.main.setup.line_1"
            ]
            part_customize
            guibar
            guistayopen [
                guiright [
                    if (=s $__main_setup_notice "") [
                        guibutton "t:gui.main.quit" "quit" "exit"  10
                        guistrut 1
                        guibutton "t:gui.main.options" [showgui "Options"]  "" 10
                        guistrut 16
                    ] [
                        guitext $__main_setup_notice  20
                    ]
                
                    guibutton "t:gui.main.setup.finish" [
                        if (|| (!= -1 (indexof $unnamedList (getname))) (=s (getname) "")) [
                            __main_setup_notice = (concatword "^fr" (gettrans "gui.main.setup.missing_name"))
                            sleep 5000 [ __main_setup_notice = "" ]
                        ] [
                            needSetup = 0
                        ]
                    ] "menu" 10
                ]
            ]
        ] [
            guibutton "Singleplayer Survival"                      "showgui gamesetup"
            if (& (isconnected) (> (getvote 0) 0)) [ 
                guibutton "t:gui.main.votes.show" "showgui votesetup 2"
            ]
            guibutton "Multiplayer"                   "if (isconnected) [showgui multiplayer] [showgui servers]"
            guibutton "t:gui.main.character_selection"           "showgui customize"
            if (= $editing 1) [
                guibutton "t:gui.main.editing"   "showgui editing"
            ]

            guibutton "t:gui.main.options"                       [showgui "Options"]

            if (= (getalias "wantmoviegui") 1) [
                if (= $mainmenu 1) [
                    guitext (concatword "^fd" (gettrans "gui.main.video_recorder"))       "recorder_dis"
                ] [ 
                    guibutton "t:gui.main.video_recorder"      "showgui Recorder"    (? (movierecording) "recorder_on" "recorder_off")
                ]
            ]

            if (isconnected) [
                guibutton "t:gui.main.disconnect"      "disconnect"          "exit"
            ] [
                guibutton "t:gui.main.quit"            "quit"                "exit"
            ]
        ]
    ] [
        guistayopen [
            guititle "t:gui.main.outdated.title"
            guicenter [
                guitext "t:gui.main.outdated.line_1"
            ]
            guicenter [
                guitext "t:gui.main.outdated.line_2"
            ]
            guicenter [
                guitext "t:gui.main.outdated.line_3"
            ]
            guibar
            guiright [
                guibutton "t:gui.main.quit" [ quit ] "exit"
                guistrut 35
                guibutton "t:gui.main.outdated.ignore" [ __ignore_version_warning = 1 ] "arrow_fw"
            ]
        ]
    ]
] [
    guiheader (gettrans "gui.main.title")
]