switchplayerclass = [
    if (isconnected) [
        echo "Your playerclass will be updated on your next respawn."
    ]
    playerclass $arg1
]

macro playerclassbutton [cleargui 1; switchplayerclass %1]

classiconlist = [
    "data/hud/class_offensive.png"
    "data/hud/class_defensive.png"
    "data/hud/class_stealth.png"
    "data/hud/class_medic.png"
]
weaponiconlist = [ // based on carriable/selectable weapon list in core code
    "data/hud/weap_knife.png"
    "data/hud/weap_slugshot.png"
    "data/hud/weap_assaultrifle.png"
    "data/hud/weap_rocketlauncher.png"
    "data/hud/weap_sniperrifle.png"
    "data/hud/weap_flamejet.png"
    "data/hud/weap_chainsaw.png"
    "data/hud/weap_grenadier.png"
    "data/hud/weap_healer.png"
    "data/hud/weap_rocketlauncher.png" // mortar
    "data/hud/weap_pistol.png"
]

newgui playerclass [
    __playerclassindent = 60
    __classiconindent = 4

    guilist [
        guilist [
            guitext "^fwSelect a class:"
            guistrut 0.5
            loop i (getplayerclassnum) [
                guilist [
                    guiimage (at $classiconlist $i) (playerclassbutton $i)
                    guistrut 0.25
                    guibutton (getplayerclassname $i) (playerclassbutton $i) 0
                ]
                guistrut 0.25
            ]
        ]
        guibar
        guilist [
            guilist [ guistrut $__playerclassindent ]
            pcidx = (substr $guirolloveranyaction (strlen (playerclassbutton "")))
            pcidx = (max 0 (min (- (getplayerclassnum) 1) $pcidx))
            pcmh = (getplayerclassmaxhealth $pcidx)
            pcat = (getplayerclassarmourtype $pcidx)
            pca = (getplayerclassarmour $pcidx)
            pcms = (getplayerclassmaxspeed $pcidx)

            guilist [
                guilist [
                    guistrut 4 1
                    guiimage (at $classiconlist $pcidx) [] 1 0 (? (= $pcidx 0) 0xFFCC44 (? (= $pcidx 1) 0x66AAFF (? (= $pcidx 2) 0x888888 0xFF4444)))
                ]
                guilist [
                    guitext (format "%1%2" (? (= $pcidx 0) "^fy" (? (= $pcidx 1) "^fb" (? (= $pcidx 2) "^fa" "^fr"))) (getplayerclassname $pcidx))
                    guilist [
                        guilist [
                            guistrut (divf $__playerclassindent 3) 1
                            guitext (format "Max Health: %1%2" (? (< $pcmh 100) "^fr" (? (= $pcmh 100) "^fb" "^fg")) $pcmh)
                        ]
                        guilist [
                            guistrut (divf $__playerclassindent 3) 1
                            guitext (format "Armour: %1%2" (? (= $pcat 2) "^fy" (? (= $pcat 1) "^fb" "^fg")) $pca)
                        ]
                        guilist [
                            guistrut (divf $__playerclassindent 3) 1
                            guitext (format "Max Speed: %1%2" (? (< $pcms 100) "^fr" (? (= $pcms 100) "^fb" "^fg")) $pcms)
                        ]
                    ]
                ]
            ]
            guistrut 1
            guilist [
                guilist [
                    guistrut (divf $__playerclassindent 5) 1
                    guitext "Weapons: "
                    guistrut 1
                    guitextscale 0.8 [ guitext "Name:" ]
                    guitextscale 0.6 [
                        guitext "Attack Delay:"
                        guitext "Kick Amount:"
                        guitext "Range:"
                        guitext "Power:"
                        guitext "Damage:"
                        guitext "Shot Amount:"
                    ]
                ]
                pcwnum = (getweaponsperclass)
                loop i $pcwnum [
                    pcwid = (getplayerclassweaponid $pcidx $i)
                    pcwn = (getplayerclassweaponname $pcidx $i)
                    pcwad = (getplayerclassweaponattackdelay $pcidx $i)
                    pcwka = (getplayerclassweaponkickamount $pcidx $i)
                    pcwr = (getplayerclassweaponrange $pcidx $i)
                    pcwp = (getplayerclassweaponpower $pcidx $i)
                    pcwd = (getplayerclassweapondamage $pcidx $i)
                    pcws = (getplayerclassweaponnumshots $pcidx $i)
                    guilist [
                        guistrut (divf $__playerclassindent 5) 1
                        guiimage (at $weaponiconlist $pcwid) [] 1 0
                        guitextscale 0.8 [ guitext (format "%1" $pcwn) ]
                        guitextscale 0.6 [
                            guitext (format "%1" $pcwad)
                            guitext (format "%1" $pcwka)
                            guitext (format "%1" $pcwr)
                            guitext (format "%1" $pcwp)
                            guitext (format "%1" $pcwd)
                            guitext (format "%1" $pcws)
                        ]
                    ]
                ]
            ]
        ]
    ]
    part_backFooter
]
