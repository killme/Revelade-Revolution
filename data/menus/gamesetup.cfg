menusetting _old_localrecorddemo $localrecorddemo
demobutton = [
    guibutton "Record demo" [
        if (> $localrecorddemo 0) [
            _old_localrecorddemo = $localrecorddemo
            localrecorddemo 0
        ] [
            localrecorddemo $_old_localrecorddemo
        ]
    ] (? (> $localrecorddemo 0) "checkbox_on" "checkbox_off")
    if (> $localrecorddemo 0) [
        arg1
        guibutton "Always" [
            if (> $localrecorddemo 1) [
                localrecorddemo 1
            ] [
                localrecorddemo 2
            ]
        ] (? (> $localrecorddemo 1) "checkbox_on" "checkbox_off")
    ]
]

startbotmatch = [
    if (isconnected) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $botmatchcount [addbot (+ $botmatchmaxskill 1) $botmatchminskill]
    ]
]

menustatevar __voting_for_sp 1
menustatevar curmaps $mainmaps

menusetting nextmap $lobbymap
menusetting nextmodeMP $lobbymode
menusetting nextmodeSP $lobbymode
menusetting nextmodifiers ""
menusetting showcustommaps 0

menusetting nextisbotmatch 0
menusetting botmatchcount 5
menusetting botmatchminskill 50
menusetting botmatchmaxskill 60

setNextModeName = [
    alias (? (! $__voting_for_sp) nextmodeMP nextmodeSP) $arg1
]

getNextModeName = [
    result (? (! $__voting_for_sp) $nextmodeMP $nextmodeSP)
]

haveModifier = [
    result (!= (indexof $nextmodifiers $arg1) -1)
]

getNextMode = [
    cases (getNextModeName) "ctf" [
        if (haveModifier "effic") [
            if (haveModifier "hold") [
                result $GAMEMODE_EFFICHOLD
            ] [
                if (haveModifier "protect") [
                    result $GAMEMODE_EFFICPROTECT
                ] [
                    result $GAMEMODE_EFFICCTF
                ]
            ]
        ] [
            if (haveModifier "hold") [
                result $GAMEMODE_HOLD
            ] [
                if (haveModifier "protect") [
                    result $GAMEMODE_PROTECT
                ] [
                    result $GAMEMODE_CTF
                ]
            ]
        ]
    ] "ffa" [
        if (haveModifier "effic") [
            if (haveModifier "teams") [
                result $GAMEMODE_EFFICTEAM
            ] [
                result $GAMEMODE_EFFIC
            ]
        ] [
            if (haveModifier "teams") [
                result $GAMEMODE_TEAMPLAY
            ] [
                result $GAMEMODE_FFA
            ]
        ]
    ] "coop-edit" [
        result $GAMEMODE_COOP
    ] "capture" [
        if (haveModifier "regen") [
            result $GAMEMODE_REGEN
        ] [
            result $GAMEMODE_CAPTURE
        ]
    ] "infection" [
        result $GAMEMODE_INFECTION
    ] "survival" [
        result $GAMEMODE_DMSP
    ]
    
]

alias showmapshot2 [ guiimage (concatword "packages/base/" (if (> $numargs 0) [result $arg1] [result $nextmap]) ".png") [ mode (getNextMode); map $nextmap ] 4 1 "data/gui/cube.png"]

_gamemode_partial = [
    guitext "^fgModes: "
    guilist [
        guilist [
            guiradio "^frSurvival (DMSP)"    $arg1 $GAMEMODE_DMSP           $arg2
            guiradio "^frFFA"                $arg1 $GAMEMODE_FFA            $arg2
            guiradio "^frCoop-Edit"          $arg1 $GAMEMODE_COOP           $arg2
            guiradio "^frEfficiency"         $arg1 $GAMEMODE_EFFIC          $arg2
            guiradio "^frCTF"                $arg1 $GAMEMODE_CTF            $arg2
            guiradio "^frProtect"            $arg1 $GAMEMODE_PROTECT        $arg2
            guiradio "^frHold"               $arg1 $GAMEMODE_HOLD           $arg2
            guiradio "^frInfection"          $arg1 $GAMEMODE_INFECTION      $arg2
            guiradio "^frCapture"            $arg1 $GAMEMODE_CAPTURE        $arg2
        ]

        guistrut 2

        guilist [
            //guiradio "^frSurvival (MP)"    $arg1 $GAMEMODE_SURVIVAL       $arg2
            guiradio "^frTeamplay"           $arg1 $GAMEMODE_TEAMPLAY       $arg2
            guistrut 1
            guiradio "^frEfficiency Team"    $arg1 $GAMEMODE_EFFICTEAM      $arg2
            guiradio "^frEfficiency CTF"     $arg1 $GAMEMODE_EFFICCTF       $arg2
            guiradio "^frEfficiency Protect" $arg1 $GAMEMODE_EFFICPROTECT   $arg2
            guiradio "^frEfficiency Hold"    $arg1 $GAMEMODE_EFFICHOLD      $arg2
            guistrut 1
            guiradio "^frRegen Capture"      $arg1 $GAMEMODE_REGEN          $arg2
        ]
    ]
]

loopcustommaps = [
    loopfiles $arg1 "packages/base" "ogz" $arg2
]

menusetting mapsoffseti 0
mapsgui = [
    guitextscale 0.7 [
        guilist [
            custommaps = ""

            if $showcustommaps [
                loopcustommaps curmap [
                    if (< (indexof $mainmaps $curmap) 0) [
                        custommaps = (concat $custommaps $curmap)
                    ]
                ]
            ]

            allmaps = (concat $curmaps $custommaps)
            if (=s (getNextModeName) "coop-edit") [
                allmaps = (concat $allmaps "^fother")
            ]
            allmapsnum = (listlen $allmaps)

            _max_maps = 6

            mapsoffseti = (max 0 $mapsoffseti)
            mapsoffset = (* $mapsoffseti $_max_maps)
            while [> $mapsoffset (- $allmapsnum 1) ] [
                mapsoffset = (- $mapsoffset $_max_maps)
                mapsoffseti = (- $mapsoffseti 1)
            ]

            guilist [ guistayopen [
                guitext "^fgmaps: " 0 45

                guilist [
                    guilist [
                        guistrut 12
                    ]
                    guilist [
                        num = 0
                        loop i (+ (ceilf (div $allmapsnum 3)) 1) [
                            guilist [
                                loop j 3 [
                                    value = (at $allmaps $num)
                                    num = (+ $num 1)

                                    if (&& (!=s $value "") (> $num $mapsoffset) (<= $num (+ $mapsoffset $_max_maps))) [
                                        guilist [
                                            if (=s $value "^fother") [
                                                guitext "New map" 0 10
                                                guistrut 4
                                                guifield _other_map 10 [ nextmap = $_other_map ]
                                            ] [
                                                _cmd = [
                                                    _other_map = ""
                                                    nextmap = [@@value]
                                                ]
                                                guiimage (concatword "packages/base/" $value ".png") $_cmd 2.5 1 "data/gui/cube.png"
                                                guibutton $value $_cmd 0 10
                                            ]
                                        ]
                                    ]
                                ] 
                            ]
                        ]
                    ]
                ]

                guilist [
                    guibutton "" [ mapsoffseti = (- $mapsoffseti 1) ] "arrow_bw" 5
                    guistrut 23
                    guibutton "" [ mapsoffseti = (+ $mapsoffseti 1) ] "arrow_fw" 5
                ]

                if (! $__voting_for_sp) [

                    guitext "^fgmodes: "

                    guilist [
                        _gm_button = [
                            guibutton (concatword "^fr" $arg1) [
                                setNextModeName @arg1
                                nextmodifiers = ""
                                curmaps = [@@arg2]
                            ] (? (=s (getNextModeName) $arg1) "radio_on" "radio_off")
                        ]

                        guilist [
                            _gm_button "infection" $mainmaps
                            _gm_button "ctf" $ctfmaps
                            _gm_button "capture" $capturemaps
                        ]

                        guistrut 2

                        guilist [
                            _gm_button "ffa" $mainmaps
                            _gm_button "coop-edit" $mainmaps
                            //_gm_button "survival" $mainmaps
                        ]

                        guistrut 2

                        _mod_button = [
                            guibutton (concatword "^fr" $arg1) [
                                nextmodifiers = (listtoggle $nextmodifiers @arg1)

                                if (!=s [@@arg2] "") [
                                    if (haveModifier [@@@arg1]) [
                                        curmaps = [@@@@arg2]
                                    ] [
                                        curmaps = [@@@@arg3]
                                    ]
                                ]
                            ] (? (haveModifier $arg1) "checkbox_on" "checkbox_off")
                        ]

                        guilist [
                            if (=s (getNextModeName) "capture") [
                                _mod_button "regen"
                            ]

                            if (|| (=s (getNextModeName) "ctf") (=s (getNextModeName) "ffa") (=s (getNextModeName) "hold")) [
                                _mod_button "effic"

                                if (!=s (getNextModeName) "ffa") [
                                    if (haveModifier "protect") [
                                        guitext "^fRhold" exit
                                    ] [
                                        _mod_button "hold" $capturemaps $ctfmaps
                                    ]
                                    if (haveModifier "hold") [
                                        guitext "^fRprotect" exit
                                    ] [
                                        _mod_button "protect"
                                    ]
                                ]
                            ]

                            if (|| (=s (getNextModeName) "ffa")) [
                                _mod_button "teams"
                            ]
                        ]
                    ]
                ] [
                    setNextModeName "survival"
                    nextmodifiers = ""
                    curmaps = $mainmaps
                ]
            ] ]

            guilist [
                guititle $nextmap
                guiimage (concatword "packages/base/" $nextmap ".png") [  ] 6 1 "data/gui/cube.png"
                guibar
                guicheckbox "include bots" nextisbotmatch 1 0 (if $nextisbotmatch [guionclear [startbotmatch]])
                guistayopen [ guilist [
                    demobutton [ guistrut 1 ]
                ] ]

                guicheckbox "show custom maps" showcustommaps
            ]
        ]


        guilist [
            guilist [ guistrut 4 ]
            guilist [
                guitext "^fginfo: "

                if (=s (getNextModeName) "capture") [
                    guitext "^fs^frCapture:^fD capture enemy bases to score points for your team"
                    if (haveModifier "regen") [
                        guitext "^fs^frRegen:^fD regenerate health and ammo while standing next to bases"
                    ]
                ] 

                if (=s (getNextModeName) "ctf") [
                    guitext "^fs^frCTF:^fD capture the enemy flag to score points for your team"
                    if (haveModifier "hold") [
                        guitext "^fs^frHold:^fD hold the flag for 20 seconds to score a point"
                    ]
                    if (haveModifier "protect") [
                        guitext "^fs^frProtect:^fD "
                    ]
                ]

                if (=s (getNextModeName) "ffa") [
                    guitext "^fs^frFFA:^fD kill players to score points"
                    if (haveModifier "teams") [
                        guitext "^fs^frTeams:^fD kill players from the enemy team to score points"
                    ]
                ]

                if (=s (getNextModeName) "infection") [
                    guitext "^fs^frInfection:^fD kill zombies to not be infected by them"
                ]

                if (=s (getNextModeName) "coop-edit") [
                    guitext "^fs^frCoop Edit:^fD edit and create maps"
                ]

                if (=s (getNextModeName) "survival") [
                    guitext "^fs^frSurvival:^fD survive while zombies are invading"
                ]

                if (haveModifier "effic") [
                    guitext "^fs^frEfficiency:^fD unlimited ammo but not pick ups"
                ]
            ]
        ]
    ] // guitextscale
]

gamesetupfooter = [
    text = (concat "play" (getmodename (getNextMode)) "on map" $nextmap)
    part_backFooter [
        guibutton [@@text] [
            if (&& (= @@arg1 1) isconnected) [
                disconnect
                disconnect
            ]
            mode (getNextMode)
            map $nextmap
            if (! $__voting_for_sp) [
                cleargui 1;
                sleep 10 [
                    showgui votesetup 2
                ]
            ]
        ]
    ]
]

gamesetupgui = [
    guistrut 0.5

    if (&& (= $arg1 1) isconnected) [
        guitext "^fryou will be disconnected from the server"
    ]

    gamesetupfooter
]

menusetting _demo_case_sensitive 0

newgui gamesetup [
    __voting_for_sp = 1
    guititle (gamecond)
    guinoautotab [
        (if $nextisbotmatch [guionclear [startbotmatch]] [guionclear 0])
        mapsgui
        gamesetupgui [1]

        if $nextisbotmatch [
            guitab bots
            guicomb [
                guitext "bot amount" 0 53
                guislider botmatchcount 1 32
                guitext "bot minimum skill"
                guislider botmatchminskill 50 101 [if (< $botmatchmaxskill $botmatchminskill) [botmatchmaxskill = $botmatchminskill]]
                guitext "bot maximum skill"
                guislider botmatchmaxskill 50 101 [if (> $botmatchminskill $botmatchmaxskill) [botmatchminskill = $botmatchmaxskill]]
            ]
            gamesetupgui
        ]
    ]

    _demo_per_page = 19
    _numdemos = 0
    loopfiles i $demodir "dmo" [
        _numdemos = (+ $_numdemos 1)
    ]
    if (> $_numdemos 0) [ guinoautotab [
        guitab "Demos"

        guilist [ guistayopen [
            guibutton "" [ _demofilter_raw = "" ] "exit"
            guifield _demofilter_raw 30
            guistrut 1
            guicheckbox "Case sensitive" _demo_case_sensitive
            _demofilter_raw = (getalias "_demofilter_raw")
            _demofilter = (? $_demo_case_sensitive $_demofilter_raw (tolower $_demofilter_raw))
        ] ]

        guibar

        _numdemos = 0
        loopfiles i $demodir "dmo" [
            x = (? $_demo_case_sensitive $i (tolower $i))
            if (!= (strstr $x $_demofilter) -1) [
                _numdemos = (+ $_numdemos 1)
            ]
        ]
        _demo_offset = (min (getalias "_demo_offset") (max (- $_numdemos $_demo_per_page) 0))

        guilist [
            guislider _demo_offset 0 (max (- $_numdemos $_demo_per_page) 0)
            guilist [
                _cur_offset = $_demo_offset
                _demo_left = $_demo_per_page
                loopfiles i $demodir "dmo" [
                    x = (? $_demo_case_sensitive $i (tolower $i))
                    if (!= (strstr $x $_demofilter) -1) [
                        if $_cur_offset [
                            _cur_offset = (- $_cur_offset 1)
                        ] [
                            if $_demo_left [
                                __demo_name = (substr $i 0 37)
                                if (!=s $__demo_name $i) [
                                    __demo_name = (concatword $__demo_name ...)
                                ]
                                
                                guibutton $__demo_name [ demo [@@i] ]
                                _demo_left = (- $_demo_left 1)
                            ]
                        ]
                    ]
                ]
            ]
        ]
        gamesetupfooter
    ] ]
] [
    guiheader (gamecond)
]

voteindex = 0
votenum = 0
votemenuinit = [ voteindex = 0 ]
votemenu = [
    __had_votes = 0
    loop i (getvote) [
        __had_votes = 1
        voteplayers = (getvote $i 0)
        votemode = (getvote $i 1)
        votemap = (getvote $i 2)
        voteself = 0
        loop j $voteplayers [
            if (= (getclientnum) (getvote $i 0 $j)) [
                voteself = 1 
            ]
        ]

        votecommand = [ clearvote ]

        if (! $voteself) [
            votecommand = [ mode @votemode; map @votemap ]
        ]

        gname = "unkown"

        loopGamemodes x [
            mname = (at $x 4)
            mid = (at $x 1)

            if (= $mid $votemode) [
                gname = $mname
            ]
        ]
        
        guilist [ //Horizontal
            votecolour = "^fw"
            if (= $voteself 1) [ votecolour = "^fy" ]
            
            guilist [ //Vertical
                guitext "      " "" 0
                guilist [
                    guitextscale 0.9 [
                        guibutton (format "%1%2" $votecolour $voteplayers) $votecommand 0
                    ]
                    guitextscale 0.7 [
                        if (= $voteplayers 1) [
                            guibutton (format " %1vote" $votecolour) $votecommand 0
                        ] [
                            guibutton (format " %1votes" $votecolour) $votecommand 0
                        ]
                    ]
                ]
            ]

            guitext " "

            guilist [
                guiimage (concatword "packages/base/" $votemap ".png") $votecommand 2 1 "data/gui/cube.png"
            ]

            guitext " "
            
            guilist [ //Vertical

                guibutton (concatword $gname " on " $votemap) $votecommand 0
                
                guilist [ //Horizontal
                    if (> $voteplayers 0) [
                        guitextscale 0.8 [
                            guibutton "^fwBy: " $votecommand 0
                        ]

                        pname = ""
                        pmore = 0

                        loop j $voteplayers [
                            if (> (strlen $pname) 64) [
                                pmore = (+ $pmore 1)
                            ] [
                                if (!= $j 0) [ 
                                    if (= $j (- $voteplayers 1)) [
                                        pname = (format "%1 ^faand " $pname)
                                    ] [
                                        pname = (format "%1^fa, " $pname)
                                    ]
                                ]
                                
                                pname = (format "%1^fw%2" $pname (getclientname (getvote $i 0 $j) 1))
                            ]
                        ]

                        if (> $pmore 0) [
                            pname = (format "%1 ^faand ^fy%2 ^famore" $pname $pmore)
                        ]

                        guitextscale 0.8 [
                            guibutton $pname $votecommand 0
                        ]
                    ] [
                        guitextscale 0.8 [ guibutton "^faNo current votes.." 0 ]
                    ]
                ]
            ]
        ]
    ]
    
    if (! $__had_votes) [
        guitext "There are currently no votes" "info"
    ]
]

newgui votesetup [
    __voting_for_sp = 0
    guinoautotab [
        mapsgui
        gamesetupgui [1]

        if $nextisbotmatch [
            guitab bots
            guicomb [
                guitext "bot amount" 0 53
                guislider botmatchcount 1 32
                guitext "bot minimum skill"
                guislider botmatchminskill 50 101 [if (< $botmatchmaxskill $botmatchminskill) [botmatchmaxskill = $botmatchminskill]]
                guitext "bot maximum skill"
                guislider botmatchmaxskill 50 101 [if (> $botmatchminskill $botmatchmaxskill) [botmatchminskill = $botmatchmaxskill]]
            ]
            gamesetupgui
        guitab Votes
        guistayopen [
            votemenu
        ]
    ]
] [
    if (= $guipasses 0) [
        votemenuinit
    ]
]
