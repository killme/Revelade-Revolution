entupdate = [ entset $tmpt $tmp0 $tmp1 $tmp2 $tmp3 ]

initentgui = [
  tmpt = ( et )
  tmp0 = ( ea 0 )
  tmp1 = ( ea 1 )
  tmp2 = ( ea 2 )
  tmp3 = ( ea 3 )
]

genentattributes = [
    entattributes = ""
    n = ( listlen $arg2 )
    loop i $n [
        do [
          t@i = [ guitext @(at $arg2 $i) ]
          a@i = [ guislider tmp@i @(at $arg3 (* 2 $i)) @(at $arg3 (+ 1 (* 2 $i))) entupdate ]
          entattributes = [ @@entattributes @t@@i; @a@@i; ]
        ]
    ]
]


menustatevar setting_entediting 1

newgui "Drop Entities" [
    guitext "Placement Style:"
    guiradio "To the Floor"                 entdrop 1
    guiradio "On Selection"                 entdrop 2
    guiradio "To the Floor at Selection"    entdrop 3
    guiradio "At Current Position"          entdrop 0
    guibar
    guicheckbox "Entity Snap-to-Grid"       entselsnap
    guicheckbox "Entity Selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

newgui "Triggers" [
    guibutton [Invisible]               [BTmm = -1; showgui "Trigger 2"]
    guibutton [A Carrot]                [BTmm = 23; showgui "Trigger 2"]
    guibutton [Switch #1]               [BTmm = 24; showgui "Trigger 2"]
    guibutton [Switch #2]               [BTmm = 25; showgui "Trigger 2"]
    guibutton [Door #1]                 [BTmm = 26; showgui "Trigger 2"]
    guibutton [Door #2]                 [BTmm = 27; showgui "Trigger 2"]
    guibutton [Door #3]                 [BTmm = 28; showgui "Trigger 2"]
]

newgui "Trigger 2" [
    guibutton [Animation Loop]                      [BTtt = 0; showgui "Trigger 3"]
    guibutton [Trigger Once (Popback)]              [BTtt = 1; showgui "Trigger 3"]
    guibutton [Trigger Once (Popback & Rumble)]     [BTtt = 2; showgui "Trigger 3"]
    guibutton [Trigger Once]                            [BTtt = 3; showgui "Trigger 3"]
    guibutton [Trigger Once (Rumble)]               [BTtt = 4; showgui "Trigger 3"]
    guibutton [Trigger Multiple (Popback)]          [BTtt = 5; showgui "Trigger 3"]
    guibutton [Trigger Multiple (Popback & Rumble)] [BTtt = 6; showgui "Trigger 3"]
    guibutton [Trigger Multiple]                        [BTtt = 7; showgui "Trigger 3"]
    guibutton [Trigger Multiple (Rumble)]           [BTtt = 8; showgui "Trigger 3"]
    guibutton [Door (Open Once)]                        [BTtt = 9; showgui "Trigger 3"]
    guibutton [Door (Auto Close)]                   [BTtt = 10; showgui "Trigger 3"]
    guibutton [Door (Locked)]                       [BTtt = 11; showgui "Trigger 3"]
    guibutton [Vanishing Trigger]                   [BTtt = 12; showgui "Trigger 3"]
    guibutton [Vanishing Trigger (Rumble)]          [BTtt = 13; showgui "Trigger 3"]
    guibutton [End Of Level Trigger]                    [BTtt = 29; showgui "Trigger 3"]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui "Trigger 3" [
    loop i 9 [
        tjt = (concat tag (concatword # $i))
        tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $tjt $tjc
    ]
]

newgui "New Teleport" [
    guitext "^fySelect a Slot below:"
    guistrut 0.5
    if (= $editing 1) [
        telelist = [
            guilist [
                guitext (format "Tele%1s:" $arg1)
                guitextscale 0.7 [ guitext (format "^fw^(newent tele%1 ^fs^fo#^fS^)" $arg1) ]
                guistrut 0.5
                guicenter [ guilist [ loop i 4 [
                    guibutton (format "0%1" (+ $i 1)) (format "newent tele%1 %2" $arg1 (+ $i 1))
                ] ] ]
            ]
        ]
        guilist [
            telelist "port"
            guistrut 2
            telelist "dest"
        ]
    ] [ guitext "^frEdit Mode ONLY" ]
]

newgui "Map Models" [
    guitext "^fySelect a Mapmodel below:"
    guitextscale 0.7 [ guitext (format "^fw^(newent mapmodel ^fs^fo^"^"^fS^)" $arg1) ]
    guistrut 0.5
    if (= $editing 1) [
        col = 4
        idx = (nummapmodels)
        row = (+ (div $idx $col) 1)
        guilist [ loop i $col [ guilist [ loop k $row [
            slot = (+ (* $i $row) $k)
            if (< $slot $idx) [ guibutton (format "^fs^fw%1%2%3^fS: %4" (? (< $slot 100) "^fs^fa0^fS" "") (? (< $slot 10) "^fs^fa0^fS" "") $slot (mapmodelname $slot)) (concat newent mapmodel $slot) ]
        ] ] if (< $i (- $col 1)) [ guistrut 2 ] ] ]
    ] [ guitext "^frEdit Mode ONLY" ]
]

newgui "Zombie Entities" [
    guitext "^fySelect a Zombie below:"
    guitextscale 0.7 [ guitext (format "^fw^(newent monster ^fs^fo##^fS^)" $arg1) ]
    guistrut 0.5
    if (= $editing 1) [
        guilist [
            zbutton = [ guibutton $arg1 (concat "newent monster" $arg2) (? (> $numargs 2) (concatword $arg3 ".png") "") ]
            guilist [
                guitext "Classic:"
                guitext "^"A^" Innocent:"
                guistrut 0.5
                guitext "Female:"
                guistrut 0.5
                guitext "Fast:"
                guistrut 0.5
                guitext "Nazi:"
                guistrut 0.5
                guitext "Other:"
            ]
            guistrut 1
            guilist [
                guilist [
                    zbutton "A"    10   "zclassic"; guistrut 1
                    zbutton "B"     9   "classicb_64"; guistrut 1
                    zbutton "C"     7   "classicc"; guistrut 1
                    zbutton "D"    12   "classicd_64"
                ]
                guilist [
                    zbutton "1"    13   "zclassic"; guistrut 1
                    zbutton "2"    14   "zclassic"
                ]
                guistrut 0.5
                guilist [
                    zbutton "1"     6   "femalez1_64"; guistrut 1
                    zbutton "2"    17   "femalez2"
                ]
                guistrut 0.5
                guilist [
                    zbutton "1"     0   "fast1_64"; guistrut 1
                    zbutton "2"     5   "fast2_64"
                ]
                guistrut 0.5
                guilist [
                    zbutton "1"             3  "nazi_64"; guistrut 1
                    zbutton "With Shotgun"  2  "nazishot_64"; guistrut 1
                    zbutton "On Bicycle"    1  "polder"
                ]
                guistrut 0.5
                guilist [
                    guilist [
                        zbutton "Jhon"              11  "zjhon"
                        zbutton "Heavy"             15  "heavy_64"
                    ]
                    guistrut 2
                    guilist [
                        zbutton "Skeleton"           8  "skeleton_64"
                    //  zbutton "Kugelpanzer"       16  "kugel_64"
                        zbutton "Zombie boss"        4
                    ]
                ]
            ]
        ]
    ] [ guitext "^frEdit Mode ONLY" ]
]

newentgui = [
    do [
        genentattributes [@@arg1] [@@arg2] [@@arg3]
        newgui @arg1 [
            guitext $tmpt
            guibar
            @entattributes
            guitab Type
            guilistsplit n 2 $enttypelist [
                guibutton $n [ entset @n ]
            ]
            guitab Misc
            quickeditmenu
        ]
    ]
]

loop i $enttypelength [
    newentgui (at $enttypelist $i) "" ""
]

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "yaw team" "0 360 0 2"
newentgui teleport "tag model" "0 20 0 100"
newentgui teledest "yaw tag" "0 360 0 20"
newentgui monster "yaw type" "0 360 0 7"
newentgui mapmodel "yaw model triggertype triggernum" "0 360 0 100 0 14 0 100"
newentgui envmap "radius" "0 400"
newentgui jumppad "Z Y X" "0 360 0 360 0 360"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type attr1 attr2 attr3 attr4" "0 12 0 1000 0 1000 0 1000"
newentgui flag "yaw type" "0 3600 3"
