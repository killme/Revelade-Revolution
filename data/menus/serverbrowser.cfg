menusetting serversPerPage 8

newgui servers [
    __gui_servers_amount_width = 5

    guistayopen [
        guilist [
            guibutton "Back" [cleargui 1] "arrow_bw"
            guibar
            guibutton "update server list from master server" [
                __servers_refreshing = 0
            ]
            //guibar
            //guicheckbox "search LAN" searchlan
            guibar
            guibutton "options" "showgui servbrowseropts"
        ]
        guibar
    ]

    //Prevent errors with incompatible masters
    server.port = ""
    server.host = ""
    server.mapname = "unkown map"
    server.name = ""
    server.players = "?"
    server.maxplayers = "?"
    server.gamemode = "unkown"

    i = 0
    isEmpty = 1

    maxServers = 0

    loopServers server [
        maxServers = (+ $maxServers 1)
    ]

    loopServers server [
        i = (+ $i 1)
        isEmpty = 0
        modeName = ""

        rememberServerHost $server.host

        loopGamemodes x [
            mname = (at $x 4)
            mid = (at $x 1)

            if (= $mid $server.gamemode) [
                modeName = $mname
            ]
        ]

        guilist [ //Horizontal
            _server_command = (concat "connect" $server.host $server.port)

            guilist [ //Vertical
                guitext "" "" 0
                guifont "adventure_subtitles" [
                    if (> $server.playercount 0) [
                        if (>= $server.playercount $server.maxplayers) [
                            guibutton (concatword "^f3" $server.playercount "/" $server.maxplayers) $_server_command 0 $__gui_servers_amount_width
                        ] [
                            if (> (+ $server.playercount 2) $server.maxplayers) [
                                guibutton (concatword "^fs^f2" $server.playercount "^fS/^f3" $server.maxplayers) $_server_command 0 $__gui_servers_amount_width
                            ] [
                                guibutton (concatword "^fs^f0" $server.playercount "^fS/" $server.maxplayers) $_server_command 0 $__gui_servers_amount_width
                            ]
                        ]
                    ] [
                        guibutton (concatword $server.playercount "/" $server.maxplayers) $_server_command 0 $__gui_servers_amount_width
                    ]
                ]
            ]

            guitext " "

            guilist [ //Vertical
                guiimage (concatword "packages/base/" $server.mapname ".png") $_server_command 2 1 "data/gui/cube.png"
            ]

            guitext " "

            guilist [ //Vertical
                guibutton $server.name $_server_command 0
                guibutton (concatword "^fs^f2" $server.mapname "^fS on ^fs^fg" $modeName "^fs") $_server_command 0
                guibutton (concatword $server.host ":" $server.port) $_server_command "server"
            ]
        ]

        if (= (mod $i $serversPerPage) 0) [
            _next = (+ $i 1)
            
            if (!= $i $maxServers) [
                _lastServerOnTab = (min $maxServers (+ $i $serversPerPage))

                if (!= $_lastServerOnTab $_next) [
                    guitab (concatword $_next "-" $_lastServerOnTab)
                ] [
                    guitab $_next
                ]
            ]
        ]
    ]

    if (= 1 $isEmpty) [
        guitext "Requesting list from masterserver ..."
    ]

    if (!= $__servers_refreshing 1) [
        __servers_refreshing = 1
        updatefrommaster
        sleep 50000 [
            __servers_refreshing = 0
        ]
    ]

    connectcomplete connect
] [ 
    __servers_refreshing = 0
] "Servers"

newgui servbrowseropts [
    if 0 [ //guilist [
        guilist [
            guitext "filters:" " "
            guicheckbox "full" filterfull 1 0
            guicheckbox "empty" filterempty 1 0
            guicheckbox "unknown" filterunknown 1 0
            guistrut 1
            guitext "mastermode:" " "
            guiradio "none" filtermmode 0
            guiradio "locked" filtermmode 1
            guiradio "private" filtermmode 2
            guiradio "password" filtermmode 3
        ]
        guibar
        guilist [
            guitext "sort by:" " "
            guiradio "default" sortserversby 0
            guiradio "players" sortserversby 1
            guiradio "ping" sortserversby 2
            guiradio "host" sortserversby 3
            //guiradio "port" sortserversby 4
            guiradio "description" sortserversby 5
            guiradio "game mode" sortserversby 6
            guiradio "map" sortserversby 7
        ]
    ]

    guitext "Servers per page:" ""
    guislider serversPerPage 0 20

    part_backFooter
] [
    guiheader "options"
]
