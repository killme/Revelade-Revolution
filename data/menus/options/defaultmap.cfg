newgui "Options: Game - Pick Map" [
    __had_maps = ""
    __map_rows = 20
    __map_cols = 2

    loop tabI 10 [
        // Do we need a tab?
        if (!=s "" (getalias (concatword "__maps_" $tabI "_0_0"))) [
            if (!= tabI 0) [
                guitab
            ]
            guinoautotab [
                guilist [
                    guilist [
                        guititle "Current lobby map:"
                        guiimage (concatword "packages/base/" $lobbymap ".png") [ cleargui 1 ] 5 1 "data/gui/cube.png"
                        guitext $lobbymap "info"
                        
                    ]
                
                    loop colI $__map_cols [
                        guilist [
                            loop rowI $__map_rows [
                                __mapvar = (concatword "__maps_" $tabI "_" $colI "_" $rowI)
                                __map = (getalias $__mapvar)
                                if $__map [
                                    guistayopen [
                                        guibutton $__map [ lobbymap @__map ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
                part_optionsFooter
            ]
        ]
    ]
] [
    __i = 0
    loopfilesnp curmap "packages/base" "ogz" [
        __i_onTab = (mod $__i 40)
        __tab = (div (- $__i $__i_onTab) 40)

        if (< $__i_onTab 20) [
            __i_col = 0
            __i_onCol = $__i_onTab
        ] [
            __i_col = 1
            __i_onCol = (- $__i_onTab 20)
        ]

        __mapvar = (concatword "__maps_" $__tab "_" $__i_col "_" $__i_onCol)
        alias $__mapvar $curmap
        __i = (+ $__i 1)
    ]
]