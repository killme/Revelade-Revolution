bindactions = [forward backward left right jump attack "attack 1" togglezoom saycommand sayteamcommand showscores toggleconsole screenshot edittoggle "setweapon 1" "setweapon 2" "setweapon 3" "setweapon 4" "nextweapon" "universaldelta 1" "universaldelta -1" dropflag addbot delbot]
bindnames = ["Forward" "Backward" "Left" "Right" "Jump" "Attack" "Alt-Attack" "Zoom" "All Chat" "Team Chat" "Scoreboard" "Toggle Console" "Take Screenshot" "Toggle Editing" "Weapon Slot 1" "Weapon Slot 2" "Weapon Slot 3" "Weapon Slot 4" "Weapon Switch" "Scroll Up" "Scroll Down" "Drop Flag" "Add Bot" "Delete Bot"]
joyshowbinds = (joystickactive)

keysgui = [
    nojoy = (|| (! (joystickactive)) (! $joyshowbinds))
    guicenter [ guitext "(Basic keybinds, for anything more use the 'bind' command)" ]
    guicenter [ guitext "Select action to bind and press desired keys (ESC when done):" ]
    guibar
    guistrut 0.5
    guinoautotab [
        guilistsplit n 2 $bindactions [
            newbinds = (concatword "newbinds" $i)
            guilist [
                guitext (tabify [@@(concatword "^fa" (at $bindnames $i) ":")] 5)
                @newbinds = (searchbinds [@@n] (if $nojoy [result "JOY"]))
                guilist [
                    guistrut (if $nojoy [result 20] [result 40]) 1
                    guikeyfield @newbinds (if $nojoy [result -15] [result -35]) [
                        oldbinds = (searchbinds [@@n] "JOY")
                        loop i (listlen $oldbinds) [bind (at $oldbinds $i) ""]
                        loop i (listlen $@@newbinds) [bind (at $@@@newbinds $i) [@@@@n]]
                    ]
                ]
            ]
        ] [guibar]
        guibar
        if (joystickactive) [
            guicheckbox "Show Controller Binds" joyshowbinds
        ] [ guitext "^fdShow Controller Binds" ]
    ]
]
mousegui = [
    guilist [
        guilist [
            guitext "^faSensitivity:"
            guitext "^faZoom Sensitivity:"
            guistrut 1
            guicheckbox "Invert Y-Axis" invmouse
        ]
        guistrut 1
        guilist [
            guislider sensitivity 1 100
            guislider zoomsens 1 100
            guilist [ guistrut 40 ]
            guistrut 1
            guicheckbox "Absolute Movements" mouseabsolute
        ]
        guistrut 1
        guistayopen [ guilist [
            guilist [
                guitext "^fd("
                guibutton "^fa^fEDefault: ^fc3" "sensitivity 3"
                guitext "^fd)"
            ]
            guilist [
                guitext "^fd("
                guibutton "^fa^fEDefault: ^fc1" "zoomsens 1"
                guitext "^fd)"
            ]
        ] ]
    ]
]
newgui "Options: Input" [
    guinoautotab [
        keysgui
        part_optionsFooter
        guitab "Mouse"
        mousegui
        part_optionsFooter
    ]
]
newgui "Options: Controller" [
    controllergui
    part_optionsFooter
]

// joystick

menunames = [ "joymousebutton" "joymousealt" "joymousecenter" "joymousescrollup" "joymousescrolldown" ]
joybindactions = (concat [[""]] $bindactions)
joybindnames = (concat "-none-" $bindnames)

bindtypeaction = ""
bindtypes = ["" "spec" "edit"]
findbindtype = [
    if (= $bindkeytype 0) [
        @arg2 = (getbind $arg1)
    ] [
        if (= $bindkeytype 1) [
            @arg2 = (getspecbind $arg1)
        ] [
            if (= $bindkeytype 3) [
                @arg2 = (geteditbind $arg1)
            ] [
            ]
        ]
    ]
]

setbind = [
    if (= $bindkeytype 0) [
        bind $arg1 $arg2
    ] [
        if (= $bindkeytype 1) [
            specbind $arg1 $arg2
        ] [
            if (= $bindkeytype 3) [
                editbind $arg1 $arg2
            ] [
            ]
        ]
    ]
]

buttonbind = [
    guibutton (concatword "^fa" $arg1) [
        @arg1 $contrnum; showgui "Options: Controller"
    ]
]

contrbutton = ""
contrmenu = 0
contrnum = 0
bindkeytype = 0
bindkeyname = ""
menutype = 0
newgui "Bind Button" [
    guitextbox (format "^fR%1^fa?" $contrbutton) 40 2
    if (= $bindkeytype 0) [ bindkeyname = "bind" ] [
    if (= $bindkeytype 1) [ bindkeyname = "specbind" ] [
    if (= $bindkeytype 2) [ bindkeyname = "editbind" ] [
    ] ] ]
    guitext (format "^fa%1 this button to:" $bindkeyname)
    guispacer 0.25 0.5
    guistrut 0.25
    if (= $contrmenu 1) [
        loop i (listlen $menunames) [
            menutype = (at $menunames $i)
            buttonbind $menutype
        ]
    ] [
        guilistsplit n 3 $joybindactions [
            guilist [
                guilist [
                    guibutton (concatword "^fa" (at $joybindnames $i)) [ setbind @contrbutton [@@@n]; showgui "Options: Controller" ]
                    guilist [ guistrut 20 ]
                ]
            ]
        ] [guistrut 2]
    ]
]
bindsetup = [
    contrbutton = $arg1
    contrnum = $arg2
    contrmenu = $arg3
    showgui "Bind Button" -1
]

buttonname = ""
joybutton = [
    buttonname = ""
    if (> $numargs 5) [ bindkeytype $arg6 ]
    if ($arg4 > 0) [
        loop i (listlen $menunames) [
            menutype = (at $menunames $i)
            if (= $$menutype $arg3) [ buttonname = $menutype ]
        ]
    ] [
        findbindtype @arg2 bindtypeaction
        buttonname = $bindtypeaction
    ]
    if (= $arg5 1) [ guitext (concatword "^fd^fe" $arg1 ": [ ^fa^fe" $buttonname " ^fd^fe]") "radio_off" ] [
        guibutton (concatword "^fd" $arg1 ": [ ^fa" $buttonname " ^fd]") [ bindsetup @arg2 @arg3 @arg4 ] [] "menu"
    ]
]

bindtypeoption = [
    guilist [
        if (< $numargs 3) [
            guiradio @arg1 bindkeytype $arg2
        ] [ guitext (concatword "^fd^fe" @arg1) (if (= $arg3 $arg2) [ result "radio_on" ] [ result "radio_off" ]) ]
        guilist [ guistrut $bindkeyindent ]
    ]
]
bindmenubox = [
    guilist [
        guitext "^fd^feMenu" (if (= $arg1 -1) [ result "radio_on" ] [ result "radio_off" ])
        guilist [ guistrut $bindkeyindent ]
    ]
]
bindtypetoggle = [
    bindkeyindent = 10
    guistrut 0.5
    guitext "^fRBind Key Type:"
    guilist [
        if (> $numargs 0) [
            bindtypeoption "Normal" 0 $arg1
            guibar
            bindtypeoption "Spectator" 1 $arg1
            guibar
            bindtypeoption "Editing" 3 $arg1
            bindmenubox $arg1
        ] [
            bindtypeoption "Normal" 0
            guibar
            bindtypeoption "Spectator" 1
            guibar
            bindtypeoption "Editing" 3
            bindmenubox
        ]
    ]
]

joybuttonlist = ""
joybuttonnum = 0
joybuttoninit = [ joybuttonlist = ""; joybuttonnum = 0 ]
joybuttoniter = [
    joybuttonlist = ""
    joybuttonnum = (getjoybuttons)
    loop i $joybuttonnum [
        joybuttonlist = (concat $joybuttonlist (getkeyname (+ $i 320)))
    ]
]

joyhatlist = ""
joyhatnum = 0
joyhatinit = [ joyhatlist = ""; joyhatnum = 0 ]
joyhatiter = [
    joyhatlist = ""
    joyhatnum = (* (getjoyhats) 8)
    loop i $joyhatnum [
        joyhatlist = (concat $joyhatlist (getkeyname (+ $i 360)))
    ]
]

joyaxislist = ""
joyaxisnum = 0
joyaxisinit = [ joyaxislist = ""; joyaxisnum = 0 ]
joyaxisiter = [
    joyaxislist = ""
    joyaxisnum = (* (getjoyaxis) 2)
    loop i $joyaxisnum [
        joyaxislist = (concat $joyaxislist (getkeyname (+ $i 340)))
    ]
]
joyaxisone = 1
joyaxistwo = 2

contrindent = 50
controllergui = [
    joybuttoninit; joyhatinit; joyaxisinit; joybuttoniter
    guinoautotab [ guistayopen [
        bindtypetoggle
        guistrut 1.5
        guilistsplit n 2 $joybuttonlist [
            joybutton (getkeyname (+ $i 320)) $n $i 0
            guilist [ guistrut $contrindent ]
        ] [guistrut 2]
    ] ]
    guistrut 0.5
    guibutton "^fpReset Controller Configuration" [ exec "data/joystick.cfg"; guionclear [showgui "Options: Controller"]; cleargui 1; ]
    guitab dpad
    joyhatiter
    guinoautotab [ guistayopen [
        bindtypetoggle
        guistrut 1.5
        guilistsplit n 2 $joyhatlist [
            joybutton (getkeyname (+ $i 360)) $n $i 0 (if (|| (= (mod $i 2) 1) (= $joyhatmove 1)) [result 1] [result 0])
            guilist [ guistrut $contrindent ]
        ] [guistrut 2]
        guistrut 1
        guicheckbox "Movement Only" joyhatmove 1 0 [sethatbinds]
    ] ]
    guitab analog
    joyaxisiter
    guinoautotab [ guistayopen [
        guilist [
            guilist [ bindtypetoggle 0 ]
            guistrut 1
            guitext (format "^faNotice: ^fYThe D-Pad movement is toggled ^fy%1^fY!" (if (= $joyhatmove 1) [result "ON"] [result "OFF"]))
        ]
        guistrut 1.5
        guilistsplit n 2 $joyaxislist [
            joybutton (getkeyname (+ $i 340)) $n $i 0 (if (|| (= (+ (ceilf (div $i 2)) 1) $joyfovxaxis) (= (+ (ceilf (div $i 2)) 1) $joyfovyaxis)) [result 1] [result 0]) 0
            guilist [ guistrut $contrindent ]
        ] [guistrut 2]
        guistrut 1
        guilist [
            guilist [
                guilist [
                    guitext "^fdLooking: ^faX: "
                    joyfovxaxisval = $joyfovxaxis
                    guifield joyfovxaxisval 3 [joyfovxaxis $joyfovxaxisval]
                    guitext " ^faY: "
                    joyfovyaxisval = $joyfovyaxis
                    guifield joyfovyaxisval 3 [joyfovyaxis $joyfovyaxisval]
                ]
                guistrut 0.5
                if (> (div (getjoyaxis) 2) 1) [
                    guilist [
                        guitextscale 0.7 [
                            guitext "^faStick 1 "
                            guilist [
                                guislider joyaxisone 1 (- (min (div (getjoyaxis) 2) $joyaxistwo) 1)
                                guilist [ guistrut 4 ]
                            ]
                            guistrut 0.25
                            guitext "" "arrow_fw"
                            guistrut 0.5
                            guitext "" "arrow_bw"
                            guistrut 0.25
                            guilist [
                                guislider joyaxistwo (+ (max 1 $joyaxisone) 1) (div (getjoyaxis) 2)
                                guilist [ guistrut 4 ]
                            ]
                            guitext " ^faStick 2"
                        ]
                    ]
                    guibutton "Switch Orientation" [joyswitchtoggle] "action"
                ]
                guilist [ guistrut $contrindent ]
            ]
            guilist [
                guicheckbox "Invert Y Axis" joyfovinvert
                guistayopen [
                    guilist [
                        guitext "^faSensitivity (X, Y):  ^fd("
                        guibutton "^fa^fEDefault: ^fc10, 7" "joyxsensitivity 10; joyysensitivity 7"
                        guitext "^fd)"
                    ]
                ]
                joysenslide = [
                    guilist [
                        guitext (concatword $arg1 ": ")
                        guilist [
                            guilist [ guistrut $contrindent ]
                            guislider (concatword joy $arg2 sensitivity) 1 20
                        ]
                    ]
                ]
                joysenslide X x
                joysenslide Y y
            ]
        ]
    ] ]
    guitab "Menu"
    guinoautotab [ guistayopen [
        bindtypetoggle -1
        guistrut 1.5
        guilistsplit n 2 $joybuttonlist [
            joybutton (getkeyname (+ $i 320)) $n (+ $i 1) 1 0 0
            guilist [ guistrut $contrindent ]
        ] [guistrut 2]
    ] ]
]

joyaxis1maxtemp = ""
joyaxis1mintemp = ""
joyaxis2maxtemp = ""
joyaxis2mintemp = ""
joyaxis3maxtemp = ""
joyaxis3mintemp = ""
joyaxis4maxtemp = ""
joyaxis4mintemp = ""
joyswitchtoggle = [
    if (= (* $joyaxisone 2) $joyfovyaxis) [
        joyfovyaxis (* $joyaxistwo 2)
        joyfovxaxis (- (* $joyaxistwo 2) 1)
    ] [
        joyfovyaxis (* $joyaxisone 2)
        joyfovxaxis (- (* $joyaxisone 2) 1)
    ]
    joyaxis1maxtemp = (getbind (getkeyname (+ (* $joyaxisone 4) 336)))
    joyaxis1mintemp = (getbind (getkeyname (+ (* $joyaxisone 4) 337)))
    joyaxis2maxtemp = (getbind (getkeyname (+ (* $joyaxisone 4) 338)))
    joyaxis2mintemp = (getbind (getkeyname (+ (* $joyaxisone 4) 339)))
    joyaxis3maxtemp = (getbind (getkeyname (+ (* $joyaxistwo 4) 336)))
    joyaxis3mintemp = (getbind (getkeyname (+ (* $joyaxistwo 4) 337)))
    joyaxis4maxtemp = (getbind (getkeyname (+ (* $joyaxistwo 4) 338)))
    joyaxis4mintemp = (getbind (getkeyname (+ (* $joyaxistwo 4) 339)))
    bind (concatword "JOYAXIS" (- (* $joyaxisone 2) 1) "MAX") $joyaxis3maxtemp
    bind (concatword "JOYAXIS" (- (* $joyaxisone 2) 1) "MIN") $joyaxis3mintemp
    bind (concatword "JOYAXIS" (* $joyaxisone 2) "MAX") $joyaxis4maxtemp
    bind (concatword "JOYAXIS" (* $joyaxisone 2) "MIN") $joyaxis4mintemp
    bind (concatword "JOYAXIS" (- (* $joyaxistwo 2) 1) "MAX") $joyaxis1maxtemp
    bind (concatword "JOYAXIS" (- (* $joyaxistwo 2) 1) "MIN") $joyaxis1mintemp
    bind (concatword "JOYAXIS" (* $joyaxistwo 2) "MAX") $joyaxis2maxtemp
    bind (concatword "JOYAXIS" (* $joyaxistwo 2) "MIN") $joyaxis2mintemp
]