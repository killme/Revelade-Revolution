part_graphics = [
    guitext "^faPerformance Key: ^fs^fgfast^fS, ^fs^fymoderate^fS, ^fs^frslow and pretty^fS"
    guispacer 0.25
    gfxindent = 20
    if (&& (< $shaders 0) (= $renderpath 0)) [
        guicomb [
            guicheckbox "^fyShaders" shaders 1 -1
            guilist [ guistrut $gfxindent ]
        ]
    ] [
        guilist [
            guilist [
                guicheckbox "^fyShaders" shaders
                guilist [ guistrut $gfxindent ]
            ]
            if $shaders [
                guibar; guiradio "^fgLow Detail" shaderdetail 1
                guibar; guiradio "^fyHigh Detail" shaderdetail 3
                if $hasglsl [ guibar; guicheckbox "^frGLSL Only" forceglsl ]
            ]
        ]
    ]
    guilist [
        guilist [
            guibutton "^faWater:" "blank"
            guilist [ guistrut $gfxindent ]
        ]
        guibar; guicheckbox "^fyRefraction" waterrefract
        guibar; guicheckbox "^frReflection" waterreflect
        guibar; guicheckbox "^fgCaustics" caustics
        guibar; guicheckbox "^fgAnimation" vertwater
    ]
    if (> $renderpath 0) [
        guilist [
            guilist [
                guitext "^faWaterfalls:"
                guilist [ guistrut $gfxindent ]
            ]
            guibar; guicheckbox "^fyRefraction" waterfallrefract
            guibar; guicheckbox "^fgReflection" waterfallenv
        ]
    ]
    if (= $renderpath 0) [
        guilist [
            guilist [
                guicheckbox "^frShadow Maps" shadowmap
                guilist [ guistrut $gfxindent ]
            ]
            if $shadowmap [
                guibar; guiradio "^fyMedium Quality" shadowmapsize 9 [blurshadowmap 1]
                guibar; guiradio "^frHigh Quality" shadowmapsize 10 [blurshadowmap 2]
            ] [ guibar; guicheckbox "^fgBlob Shadows" blobs ]
        ]
        if (>= $maxtmus 3) [
            guicheckbox "^fyDynamic Lights" ffdynlights 5 0
        ]
    ] [
        guilist [
            guilist [
                guicheckbox "^frSoft Shadows" shadowmap
                guilist [ guistrut $gfxindent ]
            ]
            if $shadowmap [
                guibar; guiradio "^fyMedium Quality" shadowmapsize 9 [blurshadowmap 1]
                guibar; guiradio "^frHigh Quality" shadowmapsize 10 [blurshadowmap 2]
            ] [ guibar; guicheckbox "^fgBlob Shadows" blobs ]
        ]
        if $glare [
            glarepreset = 0
            if (= $glarescale 1) [
                if (= $blurglare 4) [glarepreset = 1]
                if (= $blurglare 7) [glarepreset = 3]
            ]
            if (= $glarescale 2) [
                if (= $blurglare 3) [glarepreset = 2]
                if (= $blurglare 7) [glarepreset = 4]
            ]
            guilist [
                guilist [
                    guicheckbox "^frGlare " glare
                    guilist [ guistrut $gfxindent ]
                ]
                guibar; guiradio "^fySubtle" glarepreset 1 [blurglare 4; glarescale 1]
                guibar; guiradio "^fyGlowy" glarepreset 2 [blurglare 3; glarescale 2]
                guibar; guiradio "^frSoft" glarepreset 3 [blurglare 7; glarescale 1]
                guibar; guiradio "^frIntense" glarepreset 4 [blurglare 7; glarescale 2]
            ]
        ] [ guicheckbox "^frGlare" glare ]
    ]
    if $usetexrect [
        guilist [
            guilist [
                guicheckbox "^frMotion Blur " motionblur
                guilist [ guistrut $gfxindent ]
            ]
            if $motionblur [
                guibar; guiradio "^frSubtle" motionblurscale 0.5
                guibar; guiradio "^frModerate" motionblurscale 0.65
                guibar; guiradio "^frIntense" motionblurscale 0.8
            ]
        ]
    ]
    guilist [
        guilist [
            guicheckbox "^frGrass" grass
            guilist [ guistrut $gfxindent ]
        ]
        if $grass [
            guibar; guiradio "^fyQuick Fade" grassdist 128
            guibar; guiradio "^fyModerate Fade" grassdist 256
            guibar; guiradio "^frSlow Fade" grassdist 512
        ]
    ]
    if (> $renderpath 0) [
        guilist [
            guilist [
                guicheckbox "^fgDynamic Lights" maxdynlights 3 0
                guilist [ guistrut $gfxindent ]
            ]
            if $maxdynlights [
                guibar; guiradio "^fgMedium Quality" maxdynlights 3
                guibar; guiradio "^fyHigh Quality" maxdynlights 5
            ]
        ]
        guilist [
            guilist [
                guicheckbox "^fySoft Particles" depthfx
                guilist [ guistrut $gfxindent ]
            ]
            if $depthfx [
                guibar; guiradio "^fgLow Quality" depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1]
                guibar; guiradio "^fyMedium Quality" depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
                guibar; guiradio "^frHigh Quality" depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
            ]
        ]
    ]
    guicheckbox "^fgGlass Reflection" glassenv
    guilist [
        guilist [
            guicheckbox "^fgDecals" decals
            guilist [ guistrut $gfxindent ]
        ]
        if $decals [
            guibar; guiradio "^fgLow Quality" maxdecaltris 1024 [ decalfade 1000 ]
            guibar; guiradio "^fyMedium Quality" maxdecaltris 2048 [ decalfade 10000 ]
            guibar; guiradio "^frHigh Quality" maxdecaltris 4096 [ decalfade 60000 ]
        ]
    ]
    guilist [
        guilist [
            guicheckbox "^fgFix T-Joints" filltjoints
            guilist [ guistrut (+ $gfxindent 1) ]
        ]
        guitext "^fd(^fa^fEWorld Sparklies^fd)"
    ]
    guilist [
        guilist [
            guibutton "^faTextures:" "blank"
            guilist [ guistrut $gfxindent ]
        ]
        guibar; guiradio "^fgLow Quality" maxtexsize 256
        guibar; guiradio "^fyHigh Quality" maxtexsize 0
    ]
    guilist [
        guilist [
            guibutton "^faModels:" "blank"
            guilist [ guistrut $gfxindent ]
        ]
        guibar; guicheckbox "^fgLighting" lightmodels
        guibar; guicheckbox "^fgReflection" envmapmodels
        guibar; guicheckbox "^fgGlow" glowmodels
        if (> $renderpath 0) [
            guibar; guicheckbox "^fyBumpmap" bumpmodels
        ]
    ]
    guilist [
        guilist [
            guibutton "^faAnimations:" "blank"
            guilist [ guistrut $gfxindent ]
        ]
        guibar; guiradio "^fgMedium Quality" matskel 1
        guibar; guiradio "^fgHigh Quality" matskel 0
    ]
]
newgui "Options: Graphics" [
    guinoautotab [
        part_graphics
        part_optionsFooter
    ]
]