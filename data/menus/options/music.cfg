menusetting songs_changed 0
menusetting songs_mp ""
menusetting songs_sp ""
menusetting songs_mp_disabled ""
menusetting songs_sp_disabled ""

resetSongList = [
    songs_mp = ""
    songs_sp = ""

    loopfiles i "data/sounds/ng/" "ogg" [
        __song_name = (concatword "sounds/ng/" $i ".ogg")
        songs_mp = (concat $songs_mp $__song_name)
        songs_mp_disabled = (listdel $songs_mp_disabled $__song_name)
    ]

    loopfiles i "data/sounds/music/" "ogg" [
        __song_name = (concatword "sounds/music/" $i ".ogg")
        songs_sp = (concat $songs_sp $__song_name)
        songs_so_disabled = (listdel $songs_sp_disabled $__song_name)
    ]
]

// Fill songs_mp list with default songs
// when the user hasn't customised them
if (!= $songs_changed 1) [
    resetSongList
]

getMPSong = [
    result (at $songs_mp (rnd (listlen $songs_mp ) ) )
]

getSPSong = [
    result (at $songs_sp (rnd (listlen $songs_sp ) ) )
]

menusetting "wantWebRadio" 0
menusetting "webradioStations" []
menustatevar havingRadioStream 0
menustatevar nextSong (getSPSong)
menustatevar wasMPSong 0

playASong = [
    if $havingRadioStream [

    ] [
        if ( iszombiegame ) [
            if $wasMPSong [
                music (getSPSong)
            ] [
                music $nextSong
            ]

            nextSong = (getSPSong)
            wasMPSong = 0
        ] [
            if $wasMPSong [
                music $nextSong
            ] [
                music (getMPSong)
            ]

            nextSong = (getMPSong)
            wasMPSong = 1
        ]
    ]
]

if (getalias "__have_played_intro") [
] [
    __have_played_intro = 1
    waitForConfig [
        music "sounds/theme/OpeningTheme.ogg" [
            if $wantWebRadio [
                openRadioStream
            ] [
                music "sounds/theme/SerpentineTrek.ogg" playASong
            ]
        ]
    ]
]

openRadioStream = [
    havingRadioStream = 1
    updateWebRadio
    sleep 1000 [ 
        music "../soundstream" [
            havingRadioStream = 0
            playASong
        ]
    ]
]

newgui "Options: Music" [
    __gui_text_len = 60
    __gui_text_default_len = 14
    __song_len = 40
    guistayopen [ guinoautotab [
        guibutton "Reset song list" resetSongList
        guicomb [
            guilist [
                guitext "^faMusic volume " "" $__gui_text_len
                guitext "^fd( "
                guibutton "^fa^fEDefault: ^fc 60" "musicvol 60" "" $__gui_text_default_len
                guitext "^fd )"

            ]
            guislider musicvol
        ]
        guibar
        guititle "Multiplayer songs:"
        guilist [
            guilist [
                looplist song $songs_mp [
                    guitext $song "" $__song_len
                ]
                looplist song $songs_mp_disabled [
                    guitext (concatword "^fe" $song)
                ]
                guifield __mp_add_song 30
            ]
            guistrut 2
            guilist [
                looplist song $songs_mp [
                    guilist [
                        guibutton "play" [
                            music @song playASong
                        ]
                        guistrut 2
                        guibutton "queue" [
                            nextSong = @song
                            if ( iszombiegame ) [
                                wasMPSong = 0
                            ] [
                                wasMPSong = 1
                            ]
                        ]
                    ]
                ]
                looplist song $songs_mp_disabled [
                    guilist [
                        guibutton "play" [
                            music @song playASong
                        ]
                        guistrut 2
                        guibutton "remove" [
                            songs_mp_disabled = (listdel $songs_mp_disabled @song)
                        ]
                    ]
                ]
                
                guilist [
                    guibutton "play" [
                        music $__mp_add_song playASong
                    ]
                    guistrut 2
                    guibutton "queue" [
                        nextSong = $__mp_add_song
                        if ( iszombiegame ) [
                            wasMPSong = 0
                        ] [
                            wasMPSong = 1
                        ]
                    ]
                ]
            ]            
            guistrut 2
            guilist [
                looplist song $songs_mp [
                    guibutton "disable" [
                        songs_changed = 1
                        songs_mp = (listdel $songs_mp @song)
                        songs_mp_disabled = (concat $songs_mp_disabled @song)
                    ]
                ]
                looplist song $songs_mp_disabled [
                    guibutton "enable" [
                        songs_mp_disabled = (listdel $songs_mp_disabled @song)
                        songs_mp = (concat $songs_mp @song)
                    ]
                ]
                guibutton "add" [
                    if (=s "" $__mp_add_song) [
                    ] [
                        songs_mp_disabled = (listdel $songs_mp_disabled $__mp_add_song)
                        songs_mp = (listdel $songs_mp $__mp_add_song) //prevent duplicates
                        songs_mp = (concat $songs_mp $__mp_add_song)
                        __mp_add_song = ""
                    ]
                ]
            ]
        ]
        part_optionsFooter 1
        guitab "Singleplayer"
        guititle "Singleplayer songs:"
        guilist [
            guilist [
                looplist song $songs_sp [
                    guitext $song "" $__song_len
                ]
                looplist song $songs_sp_disabled [
                    guitext (concatword "^fe" $song)
                ]
                guifield __sp_add_song 30
            ]
            guistrut 2
            guilist [
                looplist song $songs_sp [
                    guilist [
                        guibutton "play" [
                            music @song playASong
                        ]
                        guistrut 2
                        guibutton "queue" [
                            nextSong = @song
                            if ( iszombiegame ) [
                                wasMPSong = 0
                            ] [
                                wasMPSong = 1
                            ]
                        ]
                    ]
                ]
                looplist song $songs_sp_disabled [
                    guilist [
                        guibutton "play" [
                            music @song playASong
                        ]
                        guistrut 2
                        guibutton "remove" [
                            songs_mp_disabled = (listdel $songs_mp_disabled @song)
                        ]
                    ]
                ]
                guilist [
                    guibutton "play" [
                        music $__sp_add_song playASong
                    ]
                    guistrut 2
                    guibutton "queue" [
                        nextSong = $__sp_add_song
                        if ( iszombiegame ) [
                            wasMPSong = 0
                        ] [
                            wasMPSong = 1
                        ]
                    ]
                ]
            ]            
            guistrut 2
            guilist [
                looplist song $songs_sp [
                    guibutton "disable" [
                        songs_changed = 1
                        songs_sp = (listdel $songs_sp @song)
                        songs_sp_disabled = (concat $songs_sp_disabled @song)
                    ]
                ]
                looplist song $songs_sp_disabled [
                    guibutton "enable" [
                        songs_sp_disabled = (listdel $songs_sp_disabled @song)
                        songs_sp = (concat $songs_sp @song)
                    ]
                ]
                guibutton "add" [
                    if (=s "" $__sp_add_song) [
                    ] [
                        songs_sp_disabled = (listdel $songs_sp_disabled $__sp_add_song)
                        songs_sp = (listdel $songs_sp $__sp_add_song) //prevent duplicates
                        songs_sp = (concat $songs_sp $__sp_add_song)
                        __sp_add_song = ""
                    ]
                ]
            ]
        ]
        part_optionsFooter 1
        if $options_advanced [
            guitab "Web radio"

            guilist [ guistayopen [
                guibutton "Start now" openRadioStream "" 45
                guicheckbox "Start radio when game starts." wantWebRadio
            ] ]

            guibar

            looplist x $webradioStations [
                _radio_host = (at $x 0)
                _radio_port = (at $x 1)
                _radio_path = (substr $x (strlen (concat $_radio_host $_radio_port "")))

                _is_current = (&& (=s $webRadioHost $_radio_host) (= $webRadioPort $_radio_port) (=s $webRadioUri $_radio_path))

                _pick_station = [
                    if @_is_current [
                    ] [
                        webRadioHost [@@@_radio_host]
                        webRadioPort @@_radio_port
                        webRadioUri [@@@_radio_path]
                    ]
                ]

                guilist [
                    guibutton "" $_pick_station (? $_is_current  "radio_on" "radio_off")

                    guibutton $_radio_host $_pick_station 0 30
                    guibutton $_radio_port $_pick_station 0 4
                    guistrut 1
                    guibutton $_radio_path $_pick_station 0 20
                    guistrut 1
                    guibutton "delete" [
                        _station = (format "^"%1^"" (concat @_radio_host @_radio_port @_radio_path))

                        _new_webRadioStations = []

                        looplist x $webradioStations [
                            _x_quot = (format "^"%1^"" $x)
                            if (!=s $_x_quot $_station) [
                                do [ _new_webRadioStations = [@_new_webRadioStations @_x_quot] ]
                            ]
                        ]
                        webradioStations = $_new_webRadioStations
                    ]
                ]
            ]

            guilist [
                guifield _radio_add_host 30
                guitext ":"
                guifield _radio_add_port 4
                guistrut 0.5
                guifield _radio_add_path 15
                guistrut 2
                guibutton "add" [
                    if (|| (=s "" $_radio_add_host) (=s "" $_radio_add_path)) [
                    ] [
                        _station_unquorted = (concat $_radio_add_host $_radio_add_port $_radio_add_path)
                        _station = (format "^"%1^"" $_station_unquorted)

                        _new_webRadioStations = []

                        looplist x $webradioStations [
                            _x_quot = (format "^"%1^"" $x)
                            if (!=s $_x_quot $_station) [
                                do [ _new_webRadioStations = [@_new_webRadioStations @_x_quot] ]
                            ]
                        ]
                        webradioStations = $_new_webRadioStations

                        do [ webradioStations = [@webradioStations @_station] ]
                        _radio_add_host = ""
                        _radio_add_path = ""
                        _radio_add_port = 80
                    ]
                ]
            ]


            part_optionsFooter 1
        ]
    ] ]

]
