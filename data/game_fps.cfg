// fps specific cfg stuff goes here

togglezoom = [ zoom (if (= $zoom 1) -1 1) ]
sayteamcommand = [inputcommand (if (>= $numargs 1) [result $arg1] [result ""]) [sayteam $commandbuf] "[team]"]

mapcomplete = [ complete $arg1 packages/base ogz ]
mapcomplete map

modenames = "ffa coop teamplay insta instateam effic efficteam tac tacteam capture regencapture ctf instactf protect instaprotect hold instahold efficctf efficprotect effichold collect instacollect efficcollect"
loop i (listlen $modenames) [
    mname = (at $modenames $i)
    alias $mname [ if (mode @i) [if (> $numargs 0) [map $arg1] [map]] ]
    mapcomplete $mname
]

complete importcube packages/cube cgz

demo = [ stopdemo; if (mode -1) [if (> $numargs 0) [map $arg1] [map]] ]
complete demo . dmo

allowedittoggle = [
    result 1
]

playermodelnum = 3

playermodeldir0 = "playermodels/alanharris"
playermodelname0 = "Alan Harris"
playermodelstory0 = [He is Alan Harris, an SAS operative from Britain who's survival and combat skills have kept him alive during the outbreak. He's earned a reputation as a tough guy and scavengers and bandits know to steer clear from him and anybody he is with. He is generally a "neutral" person, because he is distrustful of most people, but dosn't rob or kill unless threatened. His ultimate goal is to find other ex-military members and form a super group of zombie killers. His weapon of choice is the assault rifle also.]

playermodeldir1 = "playermodels/captainjones"
playermodelname1 = "Captain Jones"
playermodelstory1 = [Captain Jones searches the battlefields for infamy of his own, claiming it one fragger at a time.]

playermodeldir2 = "playermodels/kossebot"
playermodelname2 = "Kosse bot"
playermodelstory2 = [The Kosse Bot was created by a UN funded group as a last ditch attempt to fight the infection that was spreading across the world. The Kosse Bot has the ability to fight, repair itself, and build other robots. They look human on the outside, but unlike us, they do not need to eat, drink, or sleep and can survive in any climate. They create other bots identical to themselves if they can find tools and materials, so the result is a robotic counter-epidemic. Their fighting abilities are comparable to that of a trained human and several dishonest groups have managed to reprogram them to do their bidding. These are sometimes called "Bandit Bots" by other survivors. Many people believe that if it weren't for the Kosse Bot, humans would be entirely extinct.]
 
playermodeldir = [getalias (concatword "playermodeldir" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelicon = [getalias (concatword "playermodelicon" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelname = [getalias (concatword "playermodelname" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelstory = [getalias (concatword "playermodelstory" (if (> $numargs 0) [result $arg1] [result $playermodel]))]



getsongsmp = [
    msongsmp = ""
    loopfiles i "data/sounds/ng/" "ogg" [
        msongsmp = (concat $msongsmp $i)
    ]
]
getsongsmp
playasongmp = [ 
    music (concatword "sounds/ng/" (at $msongsmp (rnd (listlen $msongsmp ) ) ) ".ogg") playasongmp
]

getsongssp = [
    msongssp = ""
    loopfiles i "data/sounds/music/" "ogg" [
        msongssp = (concat $msongssp $i)
    ]
]
getsongssp
playasongsp = [ 
    music (concatword "data/music/" (at $msongssp (rnd (listlen $msongssp ) ) ) ".ogg") playasongsp
]


multiplayergame = 2
map_changed = [
    if ( iszombiegame ) [
        if ( != $multiplayergame 0) [
            multiplayergame = 0;
            playasongsp
        ]
    ] [
        if ( != $multiplayergame 1) [
            multiplayergame = 1;
            playasongmp
        ]
    ]
]

music "sounds/theme/SerpentineTrek.ogg"

//exec "data/botnames.cfg"