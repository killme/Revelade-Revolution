// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead
// NOTE: configuration hasn't been loaded yet at this point!

waitForConfig = [
    sleep 0 $arg1
]

//Checks if var is already set, if so, does not override
menustatevar = [
    if (haveAlias $arg1) [
    ] [
        //New var
        alias $arg1 $arg2
    ]
]

//Same as menustatevar but persists the var to config
menusetting = [
    menustatevar $arg1
    persist $arg1
]

unnamedList = [] //For detecting unnamed in other languages

ds = [
    _language = $arg1
    _string = ""
    looplist string $arg2 [
        if (=s "" $_string) [
            _string = $string
        ] [
            if (=s $_string "name.none") [
                unnamedList = (concatword $unnamedList $string " ")
            ]
            string = (strreplace $string "^^f" "^f")
            translate $_language $_string $string
            _string = ""
        ]
    ]

    alias (concatword "LANGUAGE_" $arg1 "_ICON") (concatword "data/lang/" $arg3 ".png")
]


supportedLanguages = ""
__onLanguageReload = []

waitForLanguage = [
    __onLanguageReload = [
        @__onLanguageReload
        @arg1
    ]
]

initLanguage = [
    loopfiles x "data/lang" "cfg" [
        exec (concatword "data/lang/" $x ".cfg")
        supportedLanguages = (concatword $supportedLanguages $x " ")
    ]
    __onLanguageReload
]
waitForConfig initLanguage

changelanguage = [
    if (!=s "" $arg1) [ language $arg1; ]
    initLanguage
]

menusetting languageOffset 0
menusetting flagsPerPage 3
part_languageSelector = [
    guibutton "" [
        languageOffset = (- $languageOffset 1)
            if (< $languageOffset 0) [
                languageOffset = (- (listlen $supportedLanguages) $flagsPerPage)
            ]
        ] "arrow_bw_minor"

        __flagsleft = $flagsPerPage
        __current_offset = 0

        loop i 2 [
            looplist x $supportedLanguages [
                if (< $__current_offset $languageOffset) [
                    __current_offset = (+ $__current_offset 1)
                    //skip
                ] [
                    if (> $__flagsleft 0) [
                        __flagsleft = (- $__flagsleft 1)
                        guibutton "" [ changelanguage @x; guiheader (gettrans "gui.main.title") ] (getalias (concatword "LANGUAGE_" $x "_ICON"))
                    ]
                ]
            ]
        ]

        guibutton "" [
            languageOffset = (+ $languageOffset 1)
            if (= $languageOffset (listlen $supportedLanguages)) [
                languageOffset = 0
            ]
        ] "arrow_fw_minor"
]

togglegui_buy = [if (= (m_survivalb (getmode)) 1) [togglegui buy]]
togglegui_cw = [ togglegui cw ]
togglegui_radioteam = [if (= (m_sp (getmode)) 0) [togglegui radioteamgui]]
togglegui_radio = [if (= (m_sp (getmode)) 0) [togglegui radiogui]]
togglegui_stats = [showgui stats; onrelease cleargui]

menusetting oldfloatspeed 100
menusetting shiftfloatspeed 500
menusetting shiftmodscale 10
shiftmod = [
    if (= $arg1 1) [
        oldfloatspeed = $floatspeed
        floatspeed $shiftfloatspeed
    ] [ floatspeed $oldfloatspeed ]
]
menusetting ctrlfloatspeed 50
menusetting ctrlmodscale 1
ctrlmod = [
    if (= $arg1 1) [
        oldfloatspeed = $floatspeed
        floatspeed $ctrlfloatspeed
    ] [ floatspeed $oldfloatspeed ]
]




togglemainmenu = [
    if (! (cleargui 1)) [
        if (isconnected) [
            showgui multiplayer
        ] [
            showgui main
        ]
    ]
]

bind ESCAPE [togglemainmenu]

newguitoggles = [
    newgui $arg1 [
        guilist [
            guilist [ @@@arg2 ]
            guistrut 2
            guistayopen [ guilist [
                iconsize = 0.4
                guiimage "data/icons/exit.png" [cleargui 1] $iconsize 0 "data/gui/cube.png"
                if (= $editing 1) [ guiimage "data/icons/edit.png" [showgui editing] $iconsize 0 "data/gui/cube.png" ]
                guiimage (? (joystickactive) "data/icons/controller.png" "data/icons/controller_unknown.png") (? (joystickactive) [showgui "Options: Controller"] [joystickinit]) $iconsize 0 "data/gui/cube.png"
                if (= $mainmenu 1) [
                    guiimage "data/icons/recorder_dis.png" [] $iconsize 0 "data/gui/cube.png"
                ] [ guiimage (concatword "data/icons/" (? (movierecording) "recorder_on" "recorder_off") ".png") [showgui Recorder] 0.5 0 "data/gui/cube.png" ]
            ] ]
        ]
    ] [ arg3 ] $arg4
]

guiright = [ guialign 1 [ arg1 ] ]
guicenter = [ guialign 0 [ arg1 ] ]
guispacer = [ guistrut $arg1; guibar; guistrut ( if (> $numargs 1) [result $arg2] [result $arg1] ) ]
guicomb = [ guilist [ guilist [ arg1 ] ] ]
guimergehits = [ mergehits 1; do $arg1; mergehits 0 ]

listtoggle = [
    //arg1: list
    //arg2: field
    if (!= (indexof $arg1 $arg2) -1) [
        result (listdel $arg1 $arg2)
    ] [
        result (concat $arg1 $arg2)
    ]
]

guiscrollselect = [
    //arg1: name
    //arg2: amount per page
    //arg3: list of possible values
    //arg4: callback (value = current value)
    //arg5: inbetween stuff
    //arg6: default value

    alias arg5 [ value_left = (!= $__itemsleft @arg2); @arg5 ]

    __ofset_name = (concatword "__scroll_" $arg1 "_offset")
    if (! (haveAlias $__ofset_name)) [
        alias $__ofset_name $arg6
    ]
    __offset = (getalias $__ofset_name)

    if (!=s "" $arg6) [ @arg6 ]

    __max_length = (listlen $arg3)

    guilist [
        guibutton "" [
            __offset = (- @__offset 1)
            if (< $__offset 0) [
                __offset = (- @@__max_length @@arg2)
            ]
            alias @__ofset_name $__offset
        ] "arrow_bw_minor"
        guitext $__offset
    ]

    __itemsleft = $arg2
    __current_offset = 0

    loop i 2 [
        looplist x $arg3 [
            if (< $__current_offset @@__offset) [
                __current_offset = (+ $__current_offset 1)
                //skip
            ] [
                if (> $__itemsleft 0) [
                    if (!=s "" @@@@arg5) [
                        @@@@arg5
                    ]
                    __itemsleft = (- $__itemsleft 1)
                    value = [@x];
                    @@@arg4
                ]
            ]
        ]
    ]

    guibutton "" [
        __offset = (+ @__offset 1)
        if (= $__offset @__max_length) [
            __offset = 0
        ]
        alias @__ofset_name $__offset
    ] "arrow_fw_minor"
]

gamecond = [ ? (= $mainmenu 1) (gettrans "gamecond.start") (? (> (otherclients) 0) (gettrans "gamecond.vote") (gettrans "gamecond.next")) ]

maprotation = [
    n = (listlen $arg1)
    loop i $n [
        curmap = (at $arg1 $i)
        next = (mod (+ $i 1) $n)
        nextmap = (at $arg1 $next)
        (if (= $numargs 2) [concatword nextmap_ $arg2 $curmap] [concatword nextmap_ $curmap]) = $nextmap
    ]
]

genmapitems = [
    looplist curmap $arg1 [
        guibutton $curmap (concat map $curmap) "cube"
    ]
]

mainmaps = "reinc hospital factory martiallaw grave bridge"
ctfmaps = "reinc factory martiallaw grave bridge"
capturemaps = "reinc hospital factory martiallaw grave bridge"
maprotation $mainmaps
maprotation $ctfmaps ctf_
maprotation $capturemaps capture_

completedHostNames = "localhost"

connectcomplete = [
    listcomplete $arg1 $completedHostNames
]
connectcomplete connect

rememberServerHost = [
    completedHostNames = (listdel $completedHostNames $arg1)
    completedHostNames = (concat $completedHostNames $arg1)
]

//customize related guis

menustatevar __name_editing 0
part_nameEditor = [
    if $__name_editing [
        __username = (getname)
            guifield __username $MAX_NAME_LENGTH [
                name $__username
                __name_editing = 0
            ]
    ] [
        guibutton (concatword "^f-" (getname)) [ __name_editing = 1 ] 0
    ]
]

//options gui

menusetting options_advanced 0

part_backFooter  = [
    guibar
    guilist [
        if (!=s $arg2 "") [
            guibutton "Back" [ cleargui 1 ] "arrow_bw" @arg1
            @arg2
        ] [
            guibutton "Back" [ cleargui 1 ] "arrow_bw"
        ]
    ]
]

part_optionsFooter = [
    __want_advanced_button = (= $arg1 1)
    guibar
    guilist [
        if $__want_advanced_button [
            guibutton "Back" [ cleargui 1 ] "arrow_bw" 66
            guicheckbox "advanced" options_advanced
        ] [
            guibutton "Back" [ cleargui 1 ] "arrow_bw"
        ]
    ]
]

guilistsplit = [
  guilist [
    i = 0
    n = (listlen $arg3)
    z = (div (+ $n (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $n)
        while [< $i $t] [
          push $arg1 (at $arg3 $i)
          @@@arg4
          pop $arg1
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [@arg5]
    ]
  ]
]

reloadMenus = [
    looplist dir [
        data/menus
        data/menus/customize
        data/menus/options
        data/menus/editing
    ] [
        loopfiles file $dir "cfg" [
            exec (concatword $dir "/" $file ".cfg")
        ]
    ]
]
reloadMenus

menustatevar a 0
if (= $a 0) [
    a = 1
    exec "data/tips.cfg"
]